
import com.vanniktech.maven.publish.AndroidSingleVariantLibrary
import com.vanniktech.maven.publish.SonatypeHost

//apply plugin: 'com.android.library'

plugins {
    id("com.android.library")
    id("com.vanniktech.maven.publish") version "0.30.0"
    id("signing")
}

android {
    namespace'com.lshershun.draglistview'
    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    configure(new AndroidSingleVariantLibrary("release", true, true))
    coordinates("io.github.hornet2319", "DragListView", "0.1")

    signAllPublications()

    pom {
        name = "DragListView"
        description = "Drag and drop to reorder items in a list, grid or board for Android. Based on RecyclerView. Also supports swiping items in a list. Relisted from jcenter"
        inceptionYear = "2015"
        url = "https://github.com/hornet2319/DragListView/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "lshershun"
                name = "Liubomyr Shershun"
                url = "https://github.com/hornet2319"
            }
        }
        scm {
            url = "https://github.com/hornet2319/DragListView"
            connection = "scm:git:git://github.com/hornet2319/DragListView.git"
            developerConnection = "scm:git:ssh://git@github.com/hornet2319/DragListView.git"
        }
    }
}

repositories{
    mavenCentral()
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.assertj:assertj-android:1.0.0'
}
